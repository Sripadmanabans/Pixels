apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: "com.squareup.sqldelight"
apply plugin: "dagger.hilt.android.plugin"

android {
  compileSdkVersion buildConfig.compileSdk

  defaultConfig {
    minSdkVersion buildConfig.minSdk
    targetSdkVersion buildConfig.targetSdk
    versionCode buildConfig.version.code
    versionName buildConfig.version.name

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // The following argument makes the Android Test Orchestrator run its
    // "pm clear" command after each test invocation. This command ensures
    // that the app's state is completely cleared between tests.
    testInstrumentationRunnerArguments clearPackageData: 'true'
  }

  signingConfigs {
    debug {
      storeFile file("keystore/debug.keystore")
      storePassword "pixels"
      keyAlias "pixels"
      keyPassword "pixels"
    }

    if (file("keystore/upload.keystore").exists()) {
      upload {
        storeFile file("keystore/upload.keystore")
        storePassword propOrEmpty("PIXELS_STORE_PASSWORD")
        keyAlias propOrEmpty("PIXELS_KEY_ALIAS")
        keyPassword propOrEmpty("PIXELS_KEY_PASSWORD")
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
    release {
      if (file("keystore/upload.keystore").exists()) {
        signingConfig signingConfigs.upload
      } else {
        signingConfig signingConfigs.debug
      }

      minifyEnabled true
      shrinkResources true
    }
  }

  buildFeatures {
    compose true
  }

  composeOptions {
    kotlinCompilerExtensionVersion libs.versions.compose.get()
  }

  packagingOptions {
    resources {
      exclude "META-INF/AL2.0"
      exclude "META-INF/LGPL2.1"
    }
  }

  testOptions {
    animationsDisabled true
    execution "ANDROIDX_TEST_ORCHESTRATOR"
  }

  // Lint crash fix in fragment 1.4.0-alpha03 https://issuetracker.google.com/issues/189967522
  lintOptions {
    disable "DialogFragmentCallbacksDetector"
  }
}

kotlin {
  sourceSets.all {
    languageSettings {
      useExperimentalAnnotation("kotlin.time.ExperimentalTime")
    }
  }
}

dependencies {

  implementation projects.theme.public
  implementation projects.timber.implWiring
  implementation projects.database.session.implWiring

  implementation libs.kotlin.stdlib.common

  implementation libs.androidx.activity.compose
  implementation libs.androidx.fragment.ktx
  implementation libs.androidx.core

  implementation libs.compose.animation.animation

  implementation libs.compose.foundation.foundation
  implementation libs.compose.foundation.layout

  implementation libs.compose.material.iconset
  implementation libs.compose.material.material

  implementation libs.compose.runtime.runtime
  implementation libs.compose.runtime.saveable

  implementation libs.compose.ui.ui
  implementation libs.compose.ui.text
  implementation libs.compose.ui.tooling
  implementation libs.compose.ui.unit
  implementation libs.compose.ui.util
  implementation libs.compose.ui.viewbinding

  implementation libs.androidx.lifecycle.runtime
  implementation libs.androidx.lifecycle.viewmodel.compose
  implementation libs.androidx.lifecycle.viewmodel.ktx

  implementation libs.androidx.navigation.compose

  implementation libs.coil.coil

  implementation libs.dagger.hilt.runtime
  kapt libs.dagger.hilt.compiler

  implementation libs.okhttp.client
  implementation libs.okhttp.loggingInterceptor

  implementation libs.sqlDelight.android

  implementation libs.retrofit.client

  implementation libs.byteunits

  testImplementation libs.junit.jupiter.runtime
  testRuntimeOnly libs.junit.jupiter.engine
  testImplementation libs.truth

  androidTestImplementation libs.compose.ui.test.core
  androidTestImplementation libs.compose.ui.test.junit

  androidTestImplementation libs.androidx.test.espresso.core
  androidTestImplementation libs.androidx.test.junit
  androidTestImplementation libs.androidx.test.rules
  androidTestImplementation libs.androidx.test.runner

  androidTestUtil libs.androidx.test.orchestrator
}

sqldelight {
  PixelsDb {
    packageName = "com.adjectivemonk2.pixels.database"
    sourceFolders = ["database"]
    schemaOutputDirectory = file("build/database")
    dependency project(":database:session:impl")
  }
}

kapt {
  correctErrorTypes true
}
