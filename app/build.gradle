apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: "com.squareup.sqldelight"
apply plugin: "com.squareup.anvil"

android {
  buildFeatures {
    compose true
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
    }
    release {
      if (file("keystore/upload.keystore").exists()) {
        signingConfig signingConfigs.upload
      } else {
        signingConfig signingConfigs.debug
      }

      minifyEnabled true
      shrinkResources true
    }
  }

  compileSdkVersion buildConfig.compileSdk

  composeOptions {
    kotlinCompilerExtensionVersion libs.versions.compose.get()
  }

  defaultConfig {
    minSdkVersion buildConfig.minSdk
    targetSdkVersion buildConfig.targetSdk
    versionCode buildConfig.version.code
    versionName buildConfig.version.name

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // The following argument makes the Android Test Orchestrator run its
    // "pm clear" command after each test invocation. This command ensures
    // that the app's state is completely cleared between tests.
    testInstrumentationRunnerArguments clearPackageData: "true"
  }

  packagingOptions {
    resources {
      exclude "META-INF/AL2.0"
      exclude "META-INF/LGPL2.1"
    }
  }

  signingConfigs {
    debug {
      storeFile file("keystore/debug.keystore")
      storePassword "pixels"
      keyAlias "pixels"
      keyPassword "pixels"
    }

    if (file("keystore/upload.keystore").exists()) {
      upload {
        storeFile file("keystore/upload.keystore")
        storePassword propOrEmpty("PIXELS_STORE_PASSWORD")
        keyAlias propOrEmpty("PIXELS_KEY_ALIAS")
        keyPassword propOrEmpty("PIXELS_KEY_PASSWORD")
      }
    }
  }

  testOptions {
    animationsDisabled true
    execution "ANDROIDX_TEST_ORCHESTRATOR"
  }
}

dependencies {

  implementation projects.database.session.implWiring

  implementation projects.dispatcher.implWiring

  implementation projects.network.core.implWiring
  implementation projects.network.gallery.implWiring

  implementation projects.scope.public

  implementation projects.theme.public

  implementation projects.logger.implWiring

  implementation projects.ui.galleries.android.implWiring
  implementation projects.ui.galleries.common.implWiring

  implementation libs.kotlin.stdlib.common

  implementation libs.androidx.activity.compose
  implementation libs.androidx.fragment.ktx
  implementation libs.androidx.core

  implementation libs.compose.animation.animation

  implementation libs.compose.foundation.foundation
  implementation libs.compose.foundation.layout

  implementation libs.compose.material.iconset
  implementation libs.compose.material.material

  implementation libs.compose.runtime.runtime
  implementation libs.compose.runtime.saveable

  implementation libs.compose.ui.ui
  implementation libs.compose.ui.text
  implementation libs.compose.ui.tooling
  implementation libs.compose.ui.unit
  implementation libs.compose.ui.util
  implementation libs.compose.ui.viewbinding

  implementation libs.coil.coil

  implementation libs.dagger.runtime
  kapt libs.dagger.compiler

  implementation libs.sqlDelight.android

  debugImplementation libs.leakCanary

  testImplementation libs.junit5.jupiter.api
  testRuntimeOnly libs.junit5.jupiter.runtime
  testImplementation libs.truth

  androidTestImplementation libs.compose.ui.test.core
  androidTestImplementation libs.compose.ui.test.junit

  androidTestImplementation libs.androidx.test.espresso.core
  androidTestImplementation libs.androidx.test.junit
  androidTestImplementation libs.androidx.test.rules
  androidTestRuntimeOnly libs.androidx.test.runner

  androidTestUtil libs.androidx.test.orchestrator
}

kapt {
  correctErrorTypes true
}

kotlin {
  sourceSets.all {
    languageSettings {
      optIn("kotlin.time.ExperimentalTime")
      optIn("com.squareup.workflow1.ui.WorkflowUiExperimentalApi")
    }
  }
}

sqldelight {
  PixelsDb {
    packageName = "com.adjectivemonk2.pixels.database"
    sourceFolders = ["database"]
    schemaOutputDirectory = file("build/database")
    dependency project(":database:session:impl")
  }
}
